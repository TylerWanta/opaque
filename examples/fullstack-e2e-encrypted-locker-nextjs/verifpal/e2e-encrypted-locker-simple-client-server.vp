attacker[active]

principal Alice[
	knows private locker_content
	knows private export_key
	knows private session_1_key
	knows public authentication_key_salt
	authentication_key = HKDF(authentication_key_salt, session_1_key, nil)
	generates additional_data
	ciphertext = AEAD_ENC(export_key, locker_content, additional_data)
	ciphertext_additional_data = ENC(session_1_key, additional_data)
	tag = MAC(session_1_key, CONCAT(ciphertext, ciphertext_additional_data))
]

Alice -> Server: authentication_key, ciphertext, ciphertext_additional_data, tag

principal Server[
	knows private session_1_key
	authentication_key_server = HKDF(authentication_key_salt, session_1_key, nil)
	_ = ASSERT(authentication_key_server, authentication_key)?
	_ = ASSERT(tag, MAC(session_1_key, CONCAT(ciphertext, ciphertext_additional_data)))?
	additional_data_server = DEC(session_1_key, ciphertext_additional_data)
]

queries[
	confidentiality? export_key
	confidentiality? session_1_key
	confidentiality? locker_content
	confidentiality? additional_data
	authentication? Alice -> Server: ciphertext
	authentication? Alice -> Server: ciphertext_additional_data
]
